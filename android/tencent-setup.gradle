//
//使用方法
//
//apply from: 'tencent-setup.gradle'
//

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath 'org.yaml:snakeyaml:1.17'
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply plugin: TencentSetupPlugin

class TencentSetupPlugin implements Plugin<Project> {
    @Override
    void apply(Project target) {
        target.plugins.withId('com.android.application') {
            File root_project_dir = target.rootProject.projectDir.parentFile
            File pubspec = new File(root_project_dir, 'pubspec.yaml')
            def cfg = new org.yaml.snakeyaml.Yaml().load(pubspec.text)
            target.android.defaultConfig.manifestPlaceholders += [
                    TENCENT_APP_ID: cfg.tencent_kit?.app_id
            ]
        }
    }
}
